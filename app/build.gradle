apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.wa2c.android.medoly.plugin.action.lrclyrics"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 11
        versionName "1.8.1"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix 'd'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }

    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        lintConfig file('lint.xml')
    }

    // output file
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            def newName = "MedolyLrcLyricsPlugin_${defaultConfig.versionName}"
            variant.outputs.all {
                // filename
                outputFileName = "${newName}.apk"
                // mapping
                variant.assembleProvider.get().doLast {
                    if ( variant.mappingFile != null && variant.mappingFile.exists() ) {
                        copy {
                            from variant.mappingFile
                            into outputFile.parent
                            rename { String fileName ->
                                "${newName}_mapping.txt"
                            }
                        }
                    }
                }
            }
        }
    }

}

repositories {
    google()
    jcenter()
    maven { url 'https://github.com/wa2c/medoly-library/raw/master/repository/' }
    maven { url 'https://github.com/wa2c/prefs/raw/master/repository/' }
}

dependencies {
    // Medoly library
    implementation 'com.wa2c.android:medolylibrary:1.2.7'
    // Prefs
    implementation 'com.wa2c.android:prefs:0.1.2'

    // Support library
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Kotlin
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.2.1'
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-android', version: '1.2.1'

    // Firebase
    implementation 'com.google.firebase:firebase-core:17.0.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Room
    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"

    // https://mvnrepository.com/artifact/com.jakewharton.timber/timber
    implementation group: 'com.jakewharton.timber', name: 'timber', version: '4.7.1'
    // https://mvnrepository.com/artifact/com.pranavpandey.android/dynamic-toasts
    implementation group: 'com.pranavpandey.android', name: 'dynamic-toasts', version: '2.3.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.5'
    // https://mvnrepository.com/artifact/com.norconex.language/langdetect
    implementation 'com.norconex.language:langdetect:1.3.0'

    implementation 'com.thelittlefireman:AppKillerManager:2.1.1'
}
